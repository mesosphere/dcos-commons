apply plugin: 'findbugs'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jacoco'

tasks.withType(FindBugs) {
  excludeFilter = file("$rootProject.projectDir/gradle/findbugs/excludeFilter.xml")
  maxHeapSize = '1024m'
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

checkstyle {
    configFile = file("$rootProject.projectDir/gradle/checkstyle/checkstyle.xml")
    sourceSets = [project.sourceSets.main] // disable style checks on tests
}

pmd {
    ruleSets = [
        'java-basic',
        'java-braces',
        'java-clone',
        'java-finalizers',
        'java-imports'
    ]
}

// Include unit test report in 'check'
// (jacoco itself depends on 'test')
check.dependsOn jacocoTestReport
jacocoTestReport {
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'org/apache/mesos/testing/**')
        })
    }
    reports {
        html.destination "${buildDir}/jacoco"
    }
}
