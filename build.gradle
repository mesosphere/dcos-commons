// Config shared by the dcos-commons library and the examples:

allprojects {
  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply plugin: 'maven'
  apply plugin: 'eclipse'
  apply plugin: 'idea'
  apply plugin: 'maven-publish'

  apply from: "$rootDir/gradle/quality.gradle"
  apply from: "$rootDir/gradle/spock.gradle"

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  repositories {
    mavenLocal()
    mavenCentral()
  }

  [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

  task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
  }

  idea {
    if (project.hasProperty('ideaParentDefined')) {
      project {
        jdkName = '1.8'
        languageLevel = '1.8'
        ipr {
          withXml { provider ->
            provider.node.component
              .find { it.@name == 'VcsDirectoryMappings' }
              .mapping.@vcs = 'Git'
          }
        }
      }
    }
  }

  jacocoTestReport {
    reports {
      xml.enabled false
      csv.enabled false
      html.destination "${buildDir}/jacoco"
    }
  }
}

// Config specific to the dcos-commons library:

apply plugin: 'application'

ext {
  yamlVer = "2.5.4"
  commonsCollectionsVer = "3.2.2"
  commonsIoVer = "2.4"
  antlrVer = "4.5.1-1"
  restServiceVer = "2.0.1"
  guiceVer = "3.0"
  junitVer = "4.11"
  gsonVer = "2.3.1"
  mesosVer = "0.28.0"
  slf4jVer = "1.7.10"
  logbackVer = "1.1.2"
  mockitoVer = "1.9.5"
  curatorVer = "2.9.1"
  powerMockVer = "1.6.4"
  dropwizardVer = "0.9.2"
  zookeeperVer = "3.4.8"
}

group = "mesosphere"
version = "0.3.5-SNAPSHOT"

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

publishing {
  repositories {
    maven {
      if(project.version.endsWith('-SNAPSHOT')) {
        url "s3://downloads.mesosphere.io/maven-snapshot/"
      } else {
        url "s3://downloads.mesosphere.io/maven/"
      }
      credentials(AwsCredentials) {
        accessKey System.env['AWS_ACCESS_KEY_ID']
        secretKey System.env['AWS_SECRET_ACCESS_KEY']
      }
    }
    publications {
      mavenJava(MavenPublication) {
        from components.java

        artifact sourceJar {
          classifier "sources"
        }
      }
    }
  }
}

dependencies {
  compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${yamlVer}"
  compile "commons-collections:commons-collections:${commonsCollectionsVer}"
  compile "commons-io:commons-io:${commonsIoVer}"
  compile "org.antlr:antlr4-runtime:${antlrVer}"
  compile "javax.ws.rs:javax.ws.rs-api:${restServiceVer}"
  compile "org.apache.curator:curator-framework:${curatorVer}"
  compile "org.apache.curator:curator-recipes:${curatorVer}"
  compile "org.apache.mesos:mesos:${mesosVer}"
  compile "org.apache.commons:commons-lang3:3.4"
  compile "org.apache.zookeeper:zookeeper:${zookeeperVer}"
  compile "org.slf4j:log4j-over-slf4j:${slf4jVer}"
  compile "org.slf4j:jcl-over-slf4j:${slf4jVer}"
  compile "ch.qos.logback:logback-classic:${logbackVer}"
  compile "com.google.inject:guice:${guiceVer}"
  compile "com.google.inject.extensions:guice-assistedinject:${guiceVer}"
  compile "com.google.code.gson:gson:${gsonVer}"
  testCompile "io.dropwizard:dropwizard-testing:${dropwizardVer}"
  testCompile "org.mockito:mockito-all:${mockitoVer}"
  testCompile "org.powermock:powermock-mockito-release-full:${powerMockVer}"
  testCompile "org.apache.curator:curator-test:${curatorVer}"
}

distributions {
  main {
    mainClassName = ''
    baseName = 'dcos-commons'
  }
}
